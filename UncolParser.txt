ometa UncolParser {
    // scanner

    // token :cs = spaces seq(cs),

    instruction = spaces letter+:cs
                  -> (cs.join("")),

    condition = "=" | "<>" | "<=" | ">=" | "<" | ">",

    integer = digit+:cs
              -> (parseInt(cs.join(""))),

    regType = 'b' | 's' | 'i' | 'l' | 'w' | 'f' | 'd',
    reg     = spaces regType:t integer:n
              -> [t, n],

    literal = spaces integer:value
              -> value,

    idFirst = letter | '_' | '$',
    idRest  = idFirst | digit,
    id      = idFirst:first idRest*:rest
              -> (first + rest.join("")),

    localLabel  = spaces '.' id:name
                  -> name,
    globalLabel = spaces '@' id:name
                  -> name,

    // parser

    operand = reg
            | literal
            | localLabel:name ~":" -> name
            | globalLabel:name ~(":" | "(") -> name,

    statement = "JUMP" condition:c localLabel:target
                -> ["JUMP" + c, [target]]
              | instruction:i operand*:src "->" reg:dest
                -> [i, src, dest]
              | instruction:i operand*:src
                -> [i, src]
              | localLabel:name ":"
                -> ["LABEL", name],

    function = globalLabel:name "(" reg*:args ")" ":"
               statement*:statements
               -> ["FUNCTION", name, args, statements],

    module = function*
}

var test = "
@fib (i1):
  SET 1 -> i2
  COMP i1 i2
  JUMP> .L1
  RETURN i1
.L1:
  SUB i1 i2 -> i1
  CALL @fib i1 -> i3
  SUB i1 i2 -> i1
  CALL @fib i1 -> i4
  ADD i3 i4 -> i3
  RETURN i3

@main ():
  SET 5 -> i1
  CALL @fib i1 -> i1
  RETURN i1
";

UncolParser.matchAll(test, "module");
